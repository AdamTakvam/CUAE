; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Metreos Record Agent"
!define PRODUCT_VERSION "Beta"
!define PRODUCT_PUBLISHER "Metreos Corporation"
!define PRODUCT_WEB_SITE "http://www.metreos.com"
!define LICENSE_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\RecordAgent.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define ONE_MONTH_TRIAL "FALSE"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "X:\application-suite\RecordAgent\NSIS\metreos eula.txt"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\Agent\RecordAgent.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "RecordAgentSetup.exe"
InstallDir "$PROGRAMFILES\Metreos\Record Agent"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "WinPCap 3.1" SEC01
  SetOutPath "$INSTDIR\3rdParty"
  File "S:\software\WinPcap\WinPcap_3_1.exe"
  nsExec::Exec '"$INSTDIR\3rdParty\WinPcap_3_1.exe"'
SectionEnd

Section "Record Agent" SEC02
  Call IsDotNETInstalled
  Pop $0
  StrCmp $0 1 netfound netnotfound
netnotfound:
  strCpy $1 "Microsoft .NET Framework 1.1 is required, download and install now?"
  MessageBox MB_YESNOCANCEL $1 IDNO +3 IDCANCEL +2
  ExecShell open "http://www.microsoft.com/downloads/details.aspx?FamilyID=262d25e3-f589-4842-8157-034d1e7cf3a3&displaylang=en"
  Quit
  MessageBox MB_ICONSTOP "Microsoft .NET Framework 1.1 is required!"
  Quit
netfound:
  SetOverwrite try
  SetOutPath "$INSTDIR\Agent\en-US"
  File "X:\Build\AppSuite\RecordAgent\en-US\Metreos.Toolset.ChatBox.resources.dll"
  File "X:\Build\AppSuite\RecordAgent\en-US\Metreos.Toolset.Commoni18n.resources.dll"
  SetOutPath "$INSTDIR\Agent"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Core.dll"
  File "x:\Build\AppSuite\RecordAgent\AxInterop.WMPLib.dll"
  File "X:\Build\AppSuite\RecordAgent\Interop.WMPLib.dll"
  File "X:\Build\AppSuite\RecordAgent\ICSharpCode.SharpZipLib.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.ChatBox.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.Commoni18n.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.CommonUtility.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.GraphicMenu.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.ImageButton.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.ImageListPopup.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.Interfaces.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.NotifyWindow.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.RichTextBoxEx.dll"
  File "X:\Build\AppSuite\RecordAgent\Metreos.Toolset.TenarySearchTree.dll"
  File "X:\Build\AppSuite\RecordAgent\RecordAgent.exe"
  File "X:\Build\AppSuite\RecordAgent\Homer.exe"
  File "X:\Build\AppSuite\RecordAgent\RecordAgent.exe.config"
  File "X:\application-suite\RecordAgent\NSIS\Metreos Record Agent User Guide.pdf"
  CreateDirectory "$SMPROGRAMS\Metreos Record Agent"
  CreateShortCut "$SMPROGRAMS\Metreos Record Agent\Metreos Record Agent.lnk" "$INSTDIR\Agent\RecordAgent.exe"
  CreateShortCut "$DESKTOP\Metreos Record Agent.lnk" "$INSTDIR\Agent\RecordAgent.exe"
  File "X:\Build\AppSuite\RecordAgent\XPPanel.dll"
  File "X:\Build\AppSuite\RecordAgent\XPTable.dll"
  SetOutPath "$INSTDIR\Service"
  File "X:\Build\PCapService\ACEd.dll"
  File "X:\Build\PCapService\cpp-cored.dll"
  File "X:\Build\PCapService\pcap-service.config"
  File "X:\Build\PCapService\pcap-service.exe"
  File "X:\Build\PCapService\PTLibd.dll"
  File "X:\Build\PCapService\NicSelector.exe"
  File "X:\application-suite\RecordAgent\NSIS\msvcp71d.dll"
  File "X:\application-suite\RecordAgent\NSIS\msvcr71d.dll"

  nsExec::Exec '$INSTDIR\Agent\Homer.exe'
  
  ; http://nsis.sourceforge.net/wiki/Service_Control_Manager_plugin_(install_services_and_drivers_on_NT/2K/XP)
  nsSCM::Install /NOUNLOAD "MetreosRecordAgentService" "Metreos Record Agent Service" 16 3 "$INSTDIR\Service\pcap-service.exe -s -a -f" "" "npf,nm" "" ""
  Pop $0
  
  nsExec::Exec '$INSTDIR\Service\NicSelector.exe'
  
  nsSCM::Stop "MetreosRecordAgentService"
  Pop $0
SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Metreos Record Agent\Network Adapter Selector.lnk" "$INSTDIR\Service\NicSelector.exe"
  CreateShortCut "$SMPROGRAMS\Metreos Record Agent\User Guide.lnk" "$INSTDIR\Agent\Metreos Record Agent User Guide.pdf"
  CreateShortCut "$SMPROGRAMS\Metreos Record Agent\About Metreos Record Agent.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Metreos Record Agent\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\Agent\RecordAgent.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\Agent\RecordAgent.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Function .onInstSuccess
  Call CreateGUID
  Pop $0
  StrCpy $4 "$0" 8 1
  StrCpy $5 "$0" 12 -13

  Call GetLocalTime
  Pop $1 ; day
  IntOp $1 $1 + 51
  StrCpy $1 "C$1F"

  Pop $2 ; Month
  IntOp $2 $2 + 68
  StrCpy $2 "B$2D"

  Pop $3 ; Year
  StrCpy $3 "$3" 2 2
  ; Ayyn: n is number months for trial
  ; AyyE: E is endless

  StrCmp ${ONE_MONTH_TRIAL} "TRUE" trial purchased
trial:
  ReadRegStr $R1 HKLM "${LICENSE_REGKEY}" "Components"
  StrCmp $R1 "" new old
new:
  StrCpy $3 "A$31"
  StrCpy $R0 "{$4-$1-$2-$3-$5}"
  WriteRegStr HKLM "${LICENSE_REGKEY}" "Components" "$R0"
old:
  Quit
  
purchased:
  StrCpy $3 "A$3E"
  StrCpy $R0 "{$4-$1-$2-$3-$5}"
  WriteRegStr HKLM "${LICENSE_REGKEY}" "Components" "$R0"
FunctionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "WinPCap by CACE Technologies"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Metreos Record Agent"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function .onInit
  ReadRegStr $R0 HKLM \
  "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" \
  "UninstallString"
  StrCmp $R0 "" done

  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \
  "Older version of ${PRODUCT_NAME} is on your machine, please uninstall first."
  Abort

;Run the uninstaller
uninst:
  ClearErrors
  ExecWait '$R0 _?=$INSTDIR' ;Do not copy the uninstaller to a temp file

  IfErrors no_remove_uninstaller
    ;You can either use Delete /REBOOTOK in the uninstaller or add some code
    ;here to remove to remove the uninstaller. Use a registry key to check
    ;whether the user has chosen to uninstall. If you are using an uninstaller
    ;components page, make sure all sections are uninstalled.
  no_remove_uninstaller:

done:

FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Function un.CloseRecordAgent
  Push $0
  loop:
  FindWindow $0 "WindowsForms10.Window.8.app3" "Metreos Record Agent"
  IntCmp $0 0 done
    SendMessage $0 16 0 0
    Sleep 100
    Goto loop
  done:
  Pop $0
FunctionEnd

; IsDotNETInstalled
 ;
 ; Usage:
 ;   Call IsDotNETInstalled
 ;   Pop $0
 ;   StrCmp $0 1 found.NETFramework no.NETFramework
 Function IsDotNETInstalled
   Push $0
   Push $1
   Push $2
   Push $3
   Push $4

   ReadRegStr $4 HKEY_LOCAL_MACHINE \
     "Software\Microsoft\.NETFramework" "InstallRoot"
   # remove trailing back slash
   Push $4
   Exch $EXEDIR
   Exch $EXEDIR
   Pop $4
   # if the root directory doesn't exist .NET is not installed
   IfFileExists $4 0 noDotNET

   StrCpy $0 0

   EnumStart:

     EnumRegKey $2 HKEY_LOCAL_MACHINE \
       "Software\Microsoft\.NETFramework\Policy"  $0
     IntOp $0 $0 + 1
     StrCmp $2 "" noDotNET

     StrCpy $1 0

     EnumPolicy:

       EnumRegValue $3 HKEY_LOCAL_MACHINE \
         "Software\Microsoft\.NETFramework\Policy\$2" $1
       IntOp $1 $1 + 1
        StrCmp $3 "" EnumStart
         IfFileExists "$4\$2.$3" foundDotNET EnumPolicy

   noDotNET:
     StrCpy $0 0
     Goto done

   foundDotNET:
     StrCpy $0 1

   done:
     Pop $4
     Pop $3
     Pop $2
     Pop $1
     Exch $0
 FunctionEnd

; Function Call     : Call GetLocalTime
;
;                     Pop "$Variable1"
;                       Day.
;
;                     Pop "$Variable2"
;                       Month.
;
;                     Pop "$Variable3"
;                       Year.
;
;                     Pop "$Variable4"
;                       Day of the week name.
;
;                     Pop "$Variable5"
;                       Hour.
;
;                     Pop "$Variable6"
;                       Minute.
;
;                     Pop "$Variable7"
;                       Second.
Function GetLocalTime
  # Prepare variables
  Push $0
  Push $1
  Push $2
  Push $3
  Push $4
  Push $5
  Push $6

  # Call GetLocalTime API from Kernel32.dll
  System::Call '*(&i2, &i2, &i2, &i2, &i2, &i2, &i2, &i2) i .r0'
  System::Call 'kernel32::GetLocalTime(i) i(r0)'
  System::Call '*$0(&i2, &i2, &i2, &i2, &i2, &i2, &i2, &i2)i \
  (.r4, .r5, .r3, .r6, .r2, .r1, .r0,)'

  # Day of week: convert to name
  StrCmp $3 0 0 +3
    StrCpy $3 Sunday
      Goto WeekNameEnd
  StrCmp $3 1 0 +3
    StrCpy $3 Monday
      Goto WeekNameEnd
  StrCmp $3 2 0 +3
    StrCpy $3 Tuesday
      Goto WeekNameEnd
  StrCmp $3 3 0 +3
    StrCpy $3 Wednesday
      Goto WeekNameEnd
  StrCmp $3 4 0 +3
    StrCpy $3 Thursday
      Goto WeekNameEnd
  StrCmp $3 5 0 +3
    StrCpy $3 Friday
      Goto WeekNameEnd
  StrCmp $3 6 0 +2
    StrCpy $3 Saturday
  WeekNameEnd:

  # Minute: convert to 2 digits format
	IntCmp $1 9 0 0 +2
	  StrCpy $1 '0$1'

  # Second: convert to 2 digits format
	IntCmp $0 9 0 0 +2
	  StrCpy $0 '0$0'

  # Return to user
  Exch $6
  Exch
  Exch $5
  Exch
  Exch 2
  Exch $4
  Exch 2
  Exch 3
  Exch $3
  Exch 3
  Exch 4
  Exch $2
  Exch 4
  Exch 5
  Exch $1
  Exch 5
  Exch 6
  Exch $0
  Exch 6
FunctionEnd

;Call CreateGUID
;Pop $0 ;contains GUID
Function CreateGUID
  Push $0
  Push $1
  System::Alloc 16
  System::Call 'ole32::CoCreateGuid(i sr1)'
  System::Call 'ole32::StringFromGUID2(i r1, w .r0, i ${NSIS_MAX_STRLEN})'
  System::Free $1
  Pop $1
  Exch $0
FunctionEnd

Section Uninstall
  nsSCM::Stop "MetreosRecordAgentService"
  Pop $0
  
  ; http://nsis.sourceforge.net/wiki/Processes_plug-in
  Processes::FindProcess "RecordAgent.exe"
  Pop $R0
  StrCmp $R0 "0" skip1
  Processes::KillProcess "RecordAgent.exe"
  Pop $R0
Skip1:
  Processes::FindProcess "NicSelector.exe"
  Pop $R0
  StrCmp $R0 "0" skip2
  Processes::KillProcess "NicSelector.exe"
  Pop $R0
skip2:
  ; http://nsis.sourceforge.net/wiki/Service_Control_Manager_plugin_(install_services_and_drivers_on_NT/2K/XP)
  nsSCM::Remove /NOUNLOAD "MetreosRecordAgentService"
  Pop $0

  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  
  Delete "$INSTDIR\Agent\XPTable.dll"
  Delete "$INSTDIR\Agent\XPPanel.dll"
  Delete "$INSTDIR\Agent\RecordAgent.exe"
  Delete "$INSTDIR\Agent\Homer.exe"
  Delete "$INSTDIR\Agent\Metreos.Toolset.TenarySearchTree.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.RichTextBoxEx.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.NotifyWindow.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.Interfaces.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.ImageListPopup.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.ImageButton.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.GraphicMenu.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.CommonUtility.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.Commoni18n.dll"
  Delete "$INSTDIR\Agent\Metreos.Toolset.ChatBox.dll"
  Delete "$INSTDIR\Agent\Interop.WMPLib.dll"
  Delete "$INSTDIR\Agent\en-US\Metreos.Toolset.Commoni18n.resources.dll"
  Delete "$INSTDIR\Agent\en-US\Metreos.Toolset.ChatBox.resources.dll"
  Delete "$INSTDIR\Agent\AxInterop.WMPLib.dll"
  Delete "$INSTDIR\Agent\Metreos.Core.dll"
  Delete "$INSTDIR\Agent\RecordAgent.exe.config"
  Delete "$INSTDIR\Agent\ICSharpCode.SharpZipLib.dll"
  Delete "$INSTDIR\Agent\Metreos Record Agent User Guide.pdf"
  Delete "$SMPROGRAMS\Metreos Record Agent\Uninstall.lnk"
  Delete "$SMPROGRAMS\Metreos Record Agent\About Metreos Record Agent.lnk"
  Delete "$SMPROGRAMS\Metreos Record Agent\Network Adapter Selector.lnk"
  Delete "$SMPROGRAMS\Metreos Record Agent\User Guide.lnk"
  Delete "$DESKTOP\Metreos Record Agent.lnk"
  Delete "$SMPROGRAMS\Metreos Record Agent\Metreos Record Agent.lnk"
  
  Sleep 3000
  Delete "$INSTDIR\Service\PTLibd.dll"
  Delete "$INSTDIR\Service\pcap-service.exe"
  Delete "$INSTDIR\Service\pcap-service.config"
  Delete "$INSTDIR\Service\cpp-cored.dll"
  Delete "$INSTDIR\Service\ACEd.dll"
  Delete "$INSTDIR\Service\msvcp71d.dll"
  Delete "$INSTDIR\Service\msvcr71d.dll"

  RMDir "$SMPROGRAMS\Metreos Record Agent"
  RMDir /r "$INSTDIR\Service"
  RMDir /r "$INSTDIR\Agent\en-US"
  RMDir /r "$INSTDIR\Agent"
  RMDir /r "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd